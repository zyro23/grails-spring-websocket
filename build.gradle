buildscript {
    ext {
        grailsVersion = "7.0.0-RC1"
    }
}

plugins {
    id "cloud.wondrify.asset-pipeline" version "5.0.15"
    id "groovy"
    id "java-library"
    id "maven-publish"
    id "org.apache.grails.gradle.grails-plugin" version "${grailsVersion}"
    id "signing"
}

group = "io.github.zyro23"
version = "2.7.0-SNAPSHOT"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api "com.fasterxml.jackson.core:jackson-databind"
    api "org.apache.grails:grails-controllers"
    api "org.springframework:spring-messaging"
    api "org.springframework:spring-websocket"

    integrationTestImplementation "org.springframework.boot:spring-boot-starter-test"
    integrationTestImplementation "org.springframework.boot:spring-boot-starter-web"
    integrationTestImplementation "org.spockframework:spock-core:2.4-M6-groovy-4.0"
    integrationTestImplementation "org.spockframework:spock-spring:2.4-M6-groovy-4.0"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

assets.packagePlugin = true

publishing {
    repositories {
        maven {
            name = "mavenBuild"
            url = layout.buildDirectory.dir("maven")
        }
        maven {
            name = "mavenCentralSnapshots"
            url = "https://central.sonatype.com/repository/maven-snapshots"
            credentials(PasswordCredentials)
        }
        maven {
            name = "mavenCentralStaging"
            url = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2"
            credentials(PasswordCredentials)
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
            }
            pom {
                name = "io.github.zyro23:grails-spring-websocket"
                description = "Spring WebSocket Plugin"
                url = "https://github.com/zyro23/grails-spring-websocket"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        name = "zyro"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/zyro23/grails-spring-websocket.git"
                    developerConnection = "scm:git:ssh://github.com:zyro23/grails-spring-websocket.git"
                    url = "https://github.com/zyro23/grails-spring-websocket"
                }
            }
        }
    }
}

signing {
    sign(publishing.publications.maven)
}

tasks.withType(Test) {
    useJUnitPlatform()
}

// TODO: ensure config-cache compatibility
tasks.withType(GenerateMavenPom) {
    notCompatibleWithConfigurationCache("")
}
tasks.named("configureGroovyCompiler") {
    notCompatibleWithConfigurationCache("")
}
tasks.named("configureGroovyCompilerGrailsCore") {
    notCompatibleWithConfigurationCache("")
}
tasks.named("configureGroovyCompilerGrailsPlugin") {
    notCompatibleWithConfigurationCache("")
}